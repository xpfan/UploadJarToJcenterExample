apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'//improtant
apply plugin: 'com.github.dcendents.android-maven'//for javadoc-task
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'InvalidPackage', 'HardcodedText'
        textReport true
        textOutput "stdout"
        abortOnError false
    }
}
//
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.0' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'

    // You need to build grpc-java to obtain these libraries below.
    compile 'io.grpc:grpc-okhttp:1.0.0' // CURRENT_GRPC_VERSION
    compile 'io.grpc:grpc-protobuf-lite:1.0.0' // CURRENT_GRPC_VERSION
    compile 'io.grpc:grpc-stub:1.0.0' // CURRENT_GRPC_VERSION
    compile 'javax.annotation:javax.annotation-api:1.2'
}

//important
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}

//important 自动生成pom.xml文件
version = "1.0.0"//发布的library的版本号

def siteUrl = 'https://github.com/xpfan/UploadJarToJcenterExample'      // 项目的主页(可随意)
def gitUrl = 'https://github.com/xpfan/UploadJarToJcenterExample'      // Git仓库的url(可随意)
group = "com.example.xpfan.grpc"   // 这里是groupId ,必须填写(建议填包名)

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'The library is a custom view about "Personal center consumption points" in our old version app'     //项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'xpfan'        //开发者基本信息
                        name 'fan xian ping'    //开发者基本信息
                        email 'fanxianping001@hotmail.com'   //开发者基本信息
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//生成source.jar
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    from('build/generated/source/proto/release/grpc')
    from('build/generated/source/proto/release/javalite')
    //去掉不需要打包的目录和文件
    //exclude('BuildConfig.class', 'R.class')
    //去掉R$开头的文件
    //exclude{ it.name.startsWith('R$');}
    classifier = 'sources'
}

//生成javadocJar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}



//important读取 local.properties 文件里面的 bintray.user和bintray.apikey
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())


bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        userOrg = "fanxianping1"//这个必须制定，不然会一直报404 这个是你创建账号时指定的名称
        repo = "swipeView"//仓库名称 必须在bintray上手动创建
        group = group//这个是你的groupid 唯一性
        name = "com.example.xpfan.grpc"//发布到JCenter上的项目名字，必须填写 唯一性
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}